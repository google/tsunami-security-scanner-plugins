plugins {
    id 'java-library'
}

description = 'Tsunami VulnDetector plugin for TorchServe CVE-2023-43654.'
group = 'com.google.tsunami'
version = '0.0.1-SNAPSHOT'

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    mavenCentral()
    mavenLocal()
}

def coreRepoBranch = System.getenv("GITBRANCH_TSUNAMI_CORE") ?: "stable"
def tcsRepoBranch = System.getenv("GITBRANCH_TSUNAMI_TCS") ?: "stable"

dependencies {
    implementation("com.google.tsunami:tsunami-common") {
      version { branch = "${coreRepoBranch}" }
    }
    implementation("com.google.tsunami:tsunami-plugin") {
      version { branch = "${coreRepoBranch}" }
    }
    implementation("com.google.tsunami:tsunami-proto") {
      version { branch = "${coreRepoBranch}" }
    }

    implementation "javax.inject:javax.inject:1"
    implementation "com.beust:jcommander:1.48"
    implementation "com.squareup.okhttp3:okhttp:3.12.0"

    testImplementation "com.google.truth:truth:1.4.4"
    testImplementation "com.google.truth.extensions:truth-java8-extension:1.4.4"
    testImplementation "com.google.truth.extensions:truth-proto-extension:1.4.4"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.12.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:5.18.0"
}

// Generate model.zip file and include it in the jar file.
task createModelsZip(type: Zip) {
    from 'src/main/resources/model'
    into '/'
    destinationDirectory = file("$buildDir/resources/main")
    archiveFileName = 'model.mar'
}

processResources.dependsOn createModelsZip
