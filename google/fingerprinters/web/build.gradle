plugins {
    id 'application'
    id 'java-library'
    id 'com.google.protobuf' version "0.8.14"
}

description = 'Tsunami web application fingerprinter.'
group = 'com.google.tsunami'
version = '0.0.1-SNAPSHOT'

repositories {
    /* TODO(b/210549664): reenable google mirror
    maven { // The google mirror is less flaky than mavenCentral()
        url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
     */
    mavenCentral()
    mavenLocal()
}

java {
  jar {
    exclude 'com/google/tsunami/plugins/fingerprinters/web/tools/**'
  }
}


protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.5"
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

def coreRepoBranch = System.getenv("GITBRANCH_TSUNAMI_CORE") ?: "stable"
def tcsRepoBranch = System.getenv("GITBRANCH_TSUNAMI_TCS") ?: "stable"

dependencies {
    implementation "com.google.auto.value:auto-value-annotations:1.11.0"
    implementation "com.google.flogger:flogger:0.9"
    implementation "com.google.flogger:google-extensions:0.9"
    implementation "com.google.flogger:flogger-system-backend:0.9"
    implementation "com.google.guava:guava:33.0.0-jre"
    implementation "com.google.inject:guice:6.0.0"
    implementation "org.jsoup:jsoup:1.9.2"
    implementation "com.google.protobuf:protobuf-java:3.25.5"
    implementation "com.google.protobuf:protobuf-javalite:3.25.5"
    implementation "com.google.protobuf:protobuf-java-util:3.25.5"
    implementation("com.google.tsunami:tsunami-common") {
      version { branch = "${coreRepoBranch}" }
    }
    implementation("com.google.tsunami:tsunami-plugin") {
      version { branch = "${coreRepoBranch}" }
    }
    implementation("com.google.tsunami:tsunami-proto") {
      version { branch = "${coreRepoBranch}" }
    }
    implementation "javax.inject:javax.inject:1"
    implementation "com.beust:jcommander:1.48"
    implementation "org.mockito:mockito-core:5.18.0"
    annotationProcessor "com.google.auto.value:auto-value:1.11.0"

    testImplementation "com.google.truth:truth:1.4.4"
    testImplementation "com.google.truth.extensions:truth-java8-extension:1.4.4"
    testImplementation "com.google.truth.extensions:truth-proto-extension:1.4.4"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:5.18.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.12.0"
    testAnnotationProcessor "com.google.auto.value:auto-value:1.11.0"
}

task runFingerprintUpdater(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.google.tsunami.plugins.fingerprinters.web.tools.FingerprintUpdater'
}

task runFingerprintFileConverter(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.google.tsunami.plugins.fingerprinters.web.tools.FingerprintFileConverter'
}
