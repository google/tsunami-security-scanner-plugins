plugins {
    id 'java-library'
}

description = 'CVE-2021-35464 Tsunami VulnDetector plugin.'
group = 'com.google.tsunami'
version = '0.0.1-SNAPSHOT'

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
    }
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    jar.manifest {
        attributes('Implementation-Title': name,
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Source-Compatibility': sourceCompatibility,
                'Target-Compatibility': targetCompatibility)
    }

    javadoc.options {
        encoding = 'UTF-8'
        use = true
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }

    // Log stacktrace to console when test fails.
    test {
        testLogging {
            exceptionFormat = 'full'
            showExceptions true
            showCauses true
            showStackTraces true
        }
        maxHeapSize = '1500m'
    }
}

ext {
    tsunamiVersion = 'latest.release'
    gsonVersion = '2.8.5'
    junitVersion = '4.13'
    okhttpVersion = '3.12.0'
    mockitoVersion = '2.28.2'
    truthVersion = '1.0.1'
    guavaVersion = '28.2-jre'
    guiceVersion = '4.2.3'
}

dependencies {
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.google.tsunami:tsunami-common:${tsunamiVersion}"
    implementation "com.google.tsunami:tsunami-plugin:${tsunamiVersion}"
    implementation "com.google.tsunami:tsunami-proto:${tsunamiVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testImplementation "com.google.guava:guava-testlib:${guavaVersion}"
    testImplementation "com.google.inject.extensions:guice-testlib:${guiceVersion}"
    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation "com.google.truth.extensions:truth-java8-extension:${truthVersion}"
    testImplementation "com.google.truth.extensions:truth-proto-extension:${truthVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}
